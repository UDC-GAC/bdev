set terminal postscript eps enhanced solid font 'Arial,18' color
set datafile missing '?'

set style fill solid 1 border lt -1
set boxwidth 1 relative
set style data histograms 
set style histogram 

load palette_file

set output output_file

unset key
#set key outside horiz center top
#set key autotitle columnheader

#set multiplot

set yrange [0:*]
set xrange [minx:maxx]

set title benchmark_tag noenhanced


set xlabel "Cluster size (#nodes)"
set ylabel label_y noenhanced


bs=box_size + 0 

unset xtics 
set grid noxtics front

set xtics nomirror format " " offset bs/2

do for [ii=1:words(label_xtics)] { set xtics add ( word(label_xtics,ii) ii-1 ) }

plot for [col=1:cols] input_file \
	u ($0-1-(bs*cols)/2+bs*col-bs/2):col*3-1:(bs) w boxes ls col fillstyle pattern col,  \
	for [col=1:cols] '' u ($0-1-(bs*cols)/2+bs*col-bs/2):col*3-1:col*3:col*3+1 w yerror notitle lw 2 lc 0 ps 0
